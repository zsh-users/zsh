#compdef man apropos whatis

setopt localoptions rcexpandparam

local rep expl star approx mrd

if [[ $words[1] == man ]] && (( $words[(I)-l] + $words[(I)--local-file] )); then
  _files || return 0
fi

if [[ $compstate[pattern_match] != [^*] ]]; then
  # If a string other than *, we just want correction, so no `*'.
  star='*'
fi

if [[ -n $_comp_correct ]]; then
  # If this is set, we are correcting with this many approximations.
  approx="(#a${_comp_correct})"
fi

if (( ! $#manpath )); then
  local mp
  mp=($(manpath 2>/dev/null))
  [[ "$mp" == *:* ]] && mp=( ${(s.:.)mp} )
  manpath=( $mp )
fi

(( $#manpath )) || manpath=( ${(s.:.)$(manpath 2>/dev/null)} ) ||
    manpath=( /usr/man(-/N) /(opt|usr)/(dt|share|X11R6|local)/(cat|)man(-/N) )

# `sman' is the SGML manual directory for Solaris 7.
# 1M is system administrator commands on SVR4

mrd=(${^manpath/\%L/${LANG:-En_US.ASCII}}/mandb(N))
if [[ $words[2] = (<->*|1M|l|n) ]]; then
  rep=(
  $manpath/(sman|man|cat)${words[2]}/${~approx}$PREFIX${~star}$SUFFIX.*(N:t) )
  (($#mrd)) && rep[$#rep+1]=($(awk "\$2 == \"$words[2]\" {print \$1}" $mrd))
else
  rep=( $manpath/(sman|man|cat)*/${~approx}$PREFIX${~star}$SUFFIX.*(N:t) )
  (($#mrd)) && rep[$#rep+1]=($(awk '{print $1}' $mrd))
fi


# Remove any compression suffix, then remove the minimum possible string
# beginning with .<->: that handles problem cases like files called
# `POSIX.1.5'.
(( $#rep )) && _wanted manuals expl 'manual page' \
    compadd - ${${rep%%.(bz2|z|gz|Z)}%.<->*}
